//gradle script - this is groovy 
apply (plugin: 'java')  //klammern können weggelassen werden
apply plugin: 'war' 
apply plugin: 'eclipse'  

buildscript {  //lambdas
    repositories {
        mavenCentral()
        }
    dependencies {
        classpath 'org.wisepersist:gwt-gradle-plugin:1.0.13'  // https://search.maven.org/
    }
}

apply plugin: 'gwt'

apply plugin: 'checkstyle'
checkstyle {
    toolVersion '7.8.1'
    ignoreFailures = true
    
    configFile file("config/checkstyle/checkstyle.xml")
}
checkstyleMain {
    source ='src/main/java'
}
checkstyleTest {
    source ='src/test/java'
}

apply plugin: 'findbugs'
findbugs {
    ignoreFailures = true
    toolVersion = "3.0.1"
    sourceSets=[sourceSets.main]
    excludeFilter = file("config/findbugs/findbugs.xml")
    reportsDir = file("$project.buildDir/reports/findbugs")
    effort = "max"
}
tasks.withType(FindBugs) {
    reports {
        xml.enabled true
        html.enabled false
    }
}

 

apply plugin:'pmd'
  pmd {
    ignoreFailures =true
    ruleSetFiles = files('config/pmd/pmd.xml')
}  

sourceCompatibility = '1.8'  //ab java9 -> '9' nicht '1.9'


repositories {
    mavenCentral()
    }
dependencies {

['context','beans','webmvc'].each {
    implementation "org.springframework:spring-$it:5.2.2.RELEASE"//spring transitive abhängigkeiten laden weitere .jars (context läd core)
}

implementation 'org.hibernate:hibernate-core:5.4.10.Final'  //hibernate
implementation 'org.springframework.data:spring-data-jpa:2.2.3.RELEASE' //spring data jpa
testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.5.2' //testcode
implementation 'org.postgresql:postgresql:42.2.9' //postgres
runtime 'com.google.gwt:gwt-servlet:2.8.2' //gwt, nicht zur compilezeit verfügbar


}

gwt {
    gwtVersion = '2.8.2'
    modules 'com.trivadis.books.Books'  //welches gwt modul gestartet werden soll
    maxHeapSize = '4G' //Größe der gestarteten VM
    eclipse {
        addGwtContainer = false
    }
}

test {
    useJUnitPlatform()
}
